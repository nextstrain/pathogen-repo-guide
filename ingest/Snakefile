"""
This is the main ingest Snakefile that orchestrates the full ingest workflow
and defines its default outputs.
"""

# Use default configuration values. Override with Snakemake's --configfile/--config options.
configfile: "config/defaults.yaml"

# This is the default rule that Snakemake will run when there are no specified targets.
# The default output of the ingest workflow is usually the curated metadata and sequences.
# Nextstrain maintained ingest workflows will produce metadata files with the
# standard Nextstrain fields and additional fields that are pathogen specific.
# We recommend use these standard fields in custom ingests as well to minimize
# the customizations you will need for the downstream phylogenetic workflow.
# TODO: Add link to centralized docs on standard Nextstrain metadata fields
rule all:
    input:
        "results/sequences.fasta",
        "results/metadata.tsv",


include: "rules/fetch_from_ncbi.smk"
include: "rules/curate.smk"


# We are pushing to standardize ingest workflows with Nextclade runs to include
# Nextclade outputs in our publicly hosted data. However, if a Nextclade dataset
# does not already exist, it requires curated data as input, so we are making
# Nextclade steps optional here.
#
# If Nextclade config values are included, the nextclade rules will create the
# final metadata TSV by joining the Nextclade output with the metadata.
# If Nextclade configs are not included, we rename the subset metadata TSV
# to the final metadata TSV.
if "nextclade" in config:

    include: "rules/nextclade.smk"

else:

    rule create_final_metadata:
        input:
            metadata="results/subset_metadata.tsv"
        output:
            metadata="results/metadata.tsv"
        shell:
            """
            mv {input.metadata} {output.metadata}
            """
